# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/rules.gni")
}
if (is_android || defined(is_docker)) {
  dep_base = rebase_path("./", "//")
} else {
  dep_base = rebase_path("../..", "//")
}

config("external_config") {
  include_dirs = [ "include" ]
}

config("internal_config") {
  include_dirs = [ "src" ]
}

executable("test") {

  sources = [
    "test/test.cc"
  ]

  include_dirs = [ "src" ]

  deps = [
    ":mllib",
    rebase_path("bat-native-rapidjson", dep_base),
  ]
}

group("bat-native-mllib") {
  public_deps = [
    ":mllib",
  ]

  data_deps = [
    ":bat-native-mllib-standalone",
  ]
}

static_library("bat-native-mllib-standalone") {
  complete_static_lib = true

  output_dir = root_out_dir

  configs -= [ "//build/config/compiler:thin_archive" ]

  defines = [
    "STANDALONE_BUILD",
  ]

  deps = [
    ":mllib",
  ]
}

source_set("mllib") {
  public_configs = [ ":external_config" ]
  configs += [ ":internal_config" ]

  output_name = "bat_native_mllib"

  sources = [
    "include/logistic_regression.h",
    "src/logistic_regression.cc",
    "src/linear_regression.cc",
    "include/linear_regression.h",
    "include/naive_bayes.h",
    "src/naive_bayes.cc"    
  ]

  deps = [
    rebase_path("bat-native-rapidjson", dep_base),
  ]
}
